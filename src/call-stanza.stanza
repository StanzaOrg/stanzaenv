defpackage stanzaenv/call-stanza :
  import core
  import collections
  import stanzaenv/system-api
  import stanzaenv/system-state
  import stanzaenv/versions
  import stanzaenv/paths
  import stanzaenv/system-path
  import stanzaenv/errors
  
public defn call-stanza (sys:System,
                         args:Tuple<String>,
                         system-state:SystemState) -> False :
  ;Ensure that the version is properly installed.
  val version = ensure-active-version-installed(system-state)
  ;If it's the system version.
  if version is SystemVersion :
    launch-system-stanza(sys, args, system-state)
  else :
    ;Retrieve the path to the Stanza executable.
    val stanza-dir = stanza-path(system-state, version)
    ;Ensure that the executable is there.
    val stanza-exe = stanza-exe-path(stanza-dir)
    if not file-exists?(sys,stanza-exe) :
      throw(MissingStanzaExe(stanza-dir))
    ;Launch Stanza with the appropriate STANZA_CONFIG.
    launch-stanza-exe(sys, stanza-exe, stanza-dir, args)
  
;Ensure that the active Stanza version is installed.
defn ensure-active-version-installed (state:SystemState) -> VersionSpecifier :
  match(active-version(state)) :
    (v:NamedVersion) :
      if not installed?(state, v) :
        throw(ActiveVersionNotInstalled(v))
      v
    (v:VersionSpecifier) :
      v
    (f:False) : 
      throw(NoActiveVersionSpecified())

;Retrieve the Stanza directory for the corresponding version.
defn stanza-path (state:SystemState, v:VersionSpecifier) -> String :
  match(v) :
    (v:NamedVersion) : stanza-version-path(install-path(state), v)
    (v:LocalVersion) : path(v)

;Launch the System Stanza version.
;It does this by removing the 'stanzaenv' folder from the PATH variable.
defn launch-system-stanza (sys:System,
                           args:Tuple<String>,
                           state:SystemState) -> False :
  ;Gather the PATH elements that do not correspond to stanzaenv folder.
  val new-path = to-tuple $
    for (item in path(path-status(state)), i in 0 to false) filter :
      i != stanzaenv-index(path-status(state))
  ;Set the new path, and then call stanza.
  set-system-paths(sys, new-path)
  replace-current-process(sys, args[0], args)

;Launch the Stanza executable
defn launch-stanza-exe (sys:System,
                        stanza-exe:String,
                        stanza-dir:String,
                        args:Tuple<String>) -> False :
  val new-args = to-tuple $
    for (x in args, i in 0 to false) seq :
      if i == 0 : stanza-exe
      else : x
  set-env(sys, "STANZA_CONFIG", stanza-dir)
  replace-current-process(sys, stanza-exe, new-args)