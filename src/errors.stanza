defpackage stanzaenv/errors :
  import core
  import collections
  import stanzaenv/versions
  import stz/printing-utils

public deftype StanzaEnvError <: Exception

;Occurs when an error occurs during unzipping of a zip file.
public defstruct UnzipError <: StanzaEnvError :
  zipfile:String

defmethod print (o:OutputStream, e:UnzipError) :
  print(o, "Error extracting contents of zip file %~." % [zipfile(e)])

;Occurs when an error occurs when downloading from a URL.
public defstruct DownloadError <: StanzaEnvError :
  url:String
  exception:Exception

defmethod print (o:OutputStream, e:DownloadError) :
  print(o, "Error downloading from %~. %_." % [url(e), exception(e)])

;Occurs when CURL returns a bad response code after downloading
;from a URL.
public defstruct BadResponseCode <: StanzaEnvError :
  url:String
  code:Long

defmethod print (o:OutputStream, e:BadResponseCode) :
  print(o, "Error downloading from %~. Error code %_." % [url(e), code(e)])

;Occurs when a STANZAENV_DIR installation is corrupted.
public defstruct BadConfigInstallation <: StanzaEnvError :
  path:String

defmethod print (o:OutputStream, e:BadConfigInstallation) :
  val msg = "The stanzaenv installation pointed to by STANZAENV_DIR at %~ is corrupted. \
             No 'stanzaenv' executable found."
  print(o, msg % [path(e)])

;Occurs when a default installation is corrupted.
public defstruct BadDefaultInstallation <: StanzaEnvError :
  path:String
  
defmethod print (o:OutputStream, e:BadDefaultInstallation) :
  val msg = "The stanzaenv installation at the default location %~ is corrupted. \
             No 'stanzaenv' executable found."
  print(o, msg % [path(e)])

;Occurs when we were unable to detect the shell
;that is running.
public defstruct CouldNotDetectShell <: StanzaEnvError
  
defmethod print (o:OutputStream, e:CouldNotDetectShell) :
  val msg = "Could not detect the current shell."
  print(o, msg)

;Occurs when we were unable to detect the user's
;HOME directory.
public defstruct CouldNotDetectHome <: StanzaEnvError
  
defmethod print (o:OutputStream, e:CouldNotDetectHome) :
  val msg = "Could not detect the user's $HOME directory."
  print(o, msg)

;Occurs when we don't know how to set the path on this
;platform yet.
public defstruct CouldNotSetPathOnPlatform <: StanzaEnvError :
  platform:Platform

defmethod print (o:OutputStream, e:CouldNotSetPathOnPlatform) :
  val msg = "Automatically setting PATH variable on platform '%_' \
             is not currently supported."
  print(o, msg % [platform(e)])

;Occurs when we don't know how to set an environment variable on this
;platform yet.
public defstruct CouldNotSetVarOnPlatform <: StanzaEnvError :
  platform:Platform

defmethod print (o:OutputStream, e:CouldNotSetVarOnPlatform) :
  val msg = "Automatically setting environment variables on platform '%_' \
             is not currently supported."
  print(o, msg % [platform(e)])

;Occurs when the configuration file is ill-formed.
public defstruct ConfigSyntaxError <: StanzaEnvError :
  info:FileInfo|False
  msg

defmethod print (o:OutputStream, e:ConfigSyntaxError) :
  val info-str = "" when info(e) is False else "%_: " % [info(e)]
  print(o, "%_%_" % [info-str, msg(e)])

;Occurs when the configuration file is ill-formed.
public defstruct ConfigFileError <: StanzaEnvError :
  filename:String
  msg

defmethod print (o:OutputStream, e:ConfigFileError) :
  print(o, "Invalid configuration file '%_': %_" % [filename(e), msg(e)])

;Occurs when 'stanzaenv' is not installed, and some operation
;was performed that depends upon it.
public defstruct StanzaenvNotInstalled <: StanzaEnvError

defmethod print (o:OutputStream, e:StanzaenvNotInstalled) :
  print(o, "Stanzaenv is not yet installed.")

;Occurs when we were unable to read the system state.
public defstruct SystemStateError <: StanzaEnvError :
  error:Exception

defmethod print (o:OutputStream, e:SystemStateError) :
  print(o, "Unable to read the system state: %_" % [error(e)])

;Occurs when we were unable to install a specific Stanza version successfully.
public defstruct StanzaInstallationError <: StanzaEnvError :
  error:Exception

defmethod print (o:OutputStream, e:StanzaInstallationError) :
  print(o, "Unable to install Stanza: %_" % [error(e)])

;Occurs when the 'stanza install' command fails.
public defstruct InstallationCommandFailed <: StanzaEnvError :
  message:String

defmethod print (o:OutputStream, e:InstallationCommandFailed) :
  print(o, "Error occurred during 'stanza install' command. %_" % [message(e)])

;Occurs when the Stanza directory does not contain the stanza executable.
public defstruct MissingStanzaExe <: StanzaEnvError :
  stanza-dir:String

defmethod print (o:OutputStream, e:MissingStanzaExe) :
  print(o, "Stanza directory '%_' is missing the 'stanza' executable" % [stanza-dir(e)]) 

;Occurs when the currently active Stanza is not installed.
public defstruct ActiveVersionNotInstalled <: StanzaEnvError :
  version:NamedVersion

defmethod print (o:OutputStream, e:ActiveVersionNotInstalled) :
  print(o, "The currently active Stanza version is %_, but this version is not yet installed." % [version(e)]) 

;Occurs when there is no Stanza version specified.
public defstruct NoActiveVersionSpecified <: StanzaEnvError

defmethod print (o:OutputStream, e:NoActiveVersionSpecified) :
  print(o, "No active Stanza version has been set.")

;Occurs when we were unable to read the system state.
public defstruct ConfigChangeError <: StanzaEnvError :
  error:Exception

defmethod print (o:OutputStream, e:ConfigChangeError) :
  print(o, "Unable to change the Stanzaenv configuration: %_" % [error(e)])

;Occurs when something wrong happens with the debug environment.
public defstruct DebugenvError <: StanzaEnvError :
  message:String

defmethod print (o:OutputStream, e:DebugenvError) :
  print(o, message(e))

;Occurs when a variable is invalid.
public defstruct InvalidDebugVar <: StanzaEnvError :
  name:String
  value:String
  error:Exception

defmethod print (o:OutputStream, e:InvalidDebugVar) :
  print(o, "Invalid value provided for debug variable %~. Provided value is %~. %_" % [
    name(e), value(e), error(e)])

;Occurs when a variable is invalid.
public defstruct DebugVarNotProvided <: StanzaEnvError :
  name:String

defmethod print (o:OutputStream, e:DebugVarNotProvided) :
  print(o, "The debug variable %~ has not been provided." % [name(e)])

;Occurs when debugging environment could not initialized
public defstruct DebugVarErrors <: StanzaEnvError :
  errors:Tuple<Exception>

defmethod print (o:OutputStream, e:DebugVarErrors) :
  print(o, "The debugging environment could not be initialized%_" % [colon-field-list(errors(e))])

;Occurs when user attempts to call an extension with a given name,
;and there is no such extension.
public defstruct NoExtension <: StanzaEnvError :
  name:String

defmethod print (o:OutputStream, e:NoExtension) :
  print(o, "There is no extended Stanza version called %~ in the current project." % [name(e)])

;Helper utility for wrapping a body and wrapping up any thrown exception.
public defn wrap-error<?T> (body:() -> ?T, Error:Exception -> Exception) -> T :
  try : body()
  catch (e) : throw(Error(e))