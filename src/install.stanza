defpackage stanzaenv/install :
  import core
  import core/parsed-path
  import collections
  import stanzaenv/system-state
  import stanzaenv/system-api
  import stanzaenv/errors
  import stanzaenv/paths
  import stanzaenv/urls
  import stanzaenv/install-version

public defn install-stanzaenv (sys:System) -> False :
  ;Check whether it's already installed.
  match(check-already-installed(sys)) :
    (install:False) :
      fresh-install(sys)
    (install:EnvInstallation) :
      report-already-installed(install)

;Check whether stanzaenv is already installed. Returns false
;if it is not. Throws an exception if the installation is
;corrupted or error occurs during confirmation.
defn check-already-installed (sys:System) -> EnvInstallation|False :
  match(get-env(sys, "STANZAENV_DIR")) :
    ;Supposed to be installed to a specific directory.
    (envdir:String) :
      if file-exists?(sys, envdir) :
        val path = relative-to(envdir, relpath-to-stanzaenv())
        if file-exists?(sys,path) : ConfigInstallation(envdir)
        else : throw(BadConfigInstallation(envdir))
    ;Supposed to be installed to the default directory.
    (f:False) :
      val dir = default-installation-dir(sys)
      match(dir:String) :
        if file-exists?(sys, dir) :
          val path = relative-to(dir, relpath-to-stanzaenv())
          if file-exists?(sys,path) : DefaultInstallation(path)
          else : throw(BadDefaultInstallation(dir))
        
;Print out a message indicating that stanzaenv is already installed.
defn report-already-installed (install:EnvInstallation) -> False :
  match(install) :
    (install:DefaultInstallation) :
      println("Stanzaenv has already been installed to its default location: %~." % [
        path(install)])
    (install:ConfigInstallation) :
      println("Stanzaenv has already been installed to the location pointed to \
               by the STANZAENV_DIR environment variable: %~." % [path(install)])

;Request an installation directory or use default.
defn request-install-dir (sys:System) -> EnvInstallation :
  let loop () :
    val msg = "Which directory should stanzaenv be installed in? Leave blank to use the default directory in $HOME: "
    val dir = prompt-user(sys, msg)
    if dir == "" :
      match(default-installation-dir(sys)) :
        (dir:String) :
          DefaultInstallation(dir)
        (f:False) :
          println("Could not determine default installation directory.")
          loop()
    else :
      match(verify-valid-install-path(sys, dir)) :
        (t:True) :
          ConfigInstallation(dir)
        (s:String) :
          println(s)
          loop()

;Verifies that the given directory is a valid installation path.
;Returns true if it is, otherwise returns a reason for why it is not.
defn verify-valid-install-path (sys:System, dir:String) -> String|True :
  match(parent-dir(dir)) :
    (p:String) :
      if file-exists?(sys, p) :
        if directory?(sys, p) :
          true
        else :
          to-string("The parent path '%_' is not a directory." % [p])
      else :
        to-string("Installation directory '%_' does not exist." % [p])
    (f:False) :
      "Improperly formatted installation directory."

;Returns the parent directory for the given path, or false if it is
;not a valid path.
defn parent-dir (dir:String) -> String|False :
  try : to-string(enclosing-dir(parse-path(dir)))
  catch (e) : false

;Request an initial version for the system to install.
defn request-initial-version (sys:System) -> NamedVersion :
  fatal("Not yet implemented")

;Perform a fresh install of the system.
defn fresh-install (sys:System) -> False :
  ;Create a temporary directory beside the appropriate location.
  ;Download the zipfile containing the stanzaenv bundle.
  ;Extract the zipfile to the appropriate location.
  ;Set up the right folders.
  ;Install the initial version of Stanza.
  val install-dir = request-install-dir(sys)
  val parent-dir = parent-dir(path(install-dir)) as String
  within temp-dirs = make-temp-dirs(sys, parent-dir) :    
    val url = stanzaenv-zip-url(current-platform(sys))
    val temp = temp-dir(temp-dirs)
    val stanzaenv-zip = relative-to(temp, "stanzaenv.zip")
    download-to-file(sys, url, stanzaenv-zip)    
    unzip-to-folder(sys, stanzaenv-zip, relative-to(temp, "stanzaenv"))
    println("Successfully installed stanzaenv to '%_'." % [path(install-dir)])

;Perform an initial install of the Stanza version.
defn install-initial-version (sys:System) -> False :
  val version = request-initial-version(sys)
  install-stanza-version(sys, version)

;Return a new path where the relative path is relative to the given directory.
defn relative-to (dir:String, path:String) -> String :
  to-string(relative-to-dir(parse-path(dir), path))