defpackage stanzaenv/install-version :
  import core
  import collections
  import stanzaenv/versions
  import stanzaenv/system-api
  import stanzaenv/system-utils
  import stanzaenv/paths
  import stanzaenv/urls
  import stanzaenv/errors

;Download the given Stanza version.
;Assumes that it's not already installed.
public defn install-stanza-version (sys:System,
                                    v:NamedVersion,
                                    root:String) -> False :
  within wrap-error(StanzaInstallationError) :
    within temp = make-temp-dir(sys, stanzaenv-temp-dir(root)) :
      val url = stanza-zipfile-url(current-platform(sys), v)
      val zip-path = relative-to(temp, "stanza.zip")
      download-to-file(sys, url, zip-path)
      unzip-to-folder(sys, zip-path, relative-to(temp, "stanza"))
      val stanza-dir = stanza-version-path(root, v)
      move-dir(sys, relative-to(temp, "stanza"), stanza-dir)
      set-executable(sys, stanza-exe-path(stanza-dir))
      run-install-command(sys, stanza-dir)
      println("Successfully installed Stanza %_." % [v])

;Run the "stanza install" command.
defn run-install-command (sys:System, stanza-dir:String) -> False :
  val stanza-exe = stanza-exe-path(stanza-dir)
  val platform-str = switch(current-platform(sys)) :
    OS-X : "os-x"
    Linux : "linux"
    Windows : "windows"
  val [ret, output] = call-system(
                        sys
                        "./stanza"
                        ["./stanza" "install" "-platform" platform-str "-path" "."]
                        stanza-dir
                        false)
  if ret != 0 :
    throw(InstallationCommandFailed(output))