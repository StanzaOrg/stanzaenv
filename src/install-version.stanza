defpackage stanzaenv/install-version :
  import core
  import collections
  import stanzaenv/versions
  import stanzaenv/system-api
  import stanzaenv/system-utils
  import stanzaenv/paths
  import stanzaenv/urls
  import stanzaenv/errors
  import stanzaenv/config
  import stanzaenv/printing

;Download the given Stanza version.
;Assumes that it's not already installed.
public defn install-stanza-version (sys:System,
                                    v:NamedVersion,
                                    root:String) -> False :
  within wrap-error(StanzaInstallationError) :
    within temp = make-temp-dir(sys, path(TempFolder(), root)) :
      val temp-stanza = relative-to(temp, "stanza")
      val url = stanza-zipfile-url(current-platform(sys), v)
      val zip-path = relative-to(temp, "stanza.zip")
      download-to-file(sys, url, zip-path)
      unzip-to-folder(sys, zip-path, temp-stanza)
      move-dir(sys, temp-stanza, path(StanzaFolder(v), root))
      set-executable(sys, path(StanzaExe(v), current-platform(sys), root))
      paragraph("Successfully installed Stanza %_." % [v])
  

