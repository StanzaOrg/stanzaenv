defpackage stanzaenv/paths :
  import core
  import core/parsed-path
  import stanzaenv/system-api
  import stanzaenv/versions

;Relative path to stanzaenv executable.
public defn relpath-to-stanzaenv () -> String :
  "bin/stanzaenv"

;Path to the stanzaenv binary directory.
public defn bin-dir (root:String) -> String :
  relative-to(root, "bin")

;Name of the stanza version configuration file.
public defn project-config-file () -> String :
  ".stanza-version"

;Name of the global stanzaenv configuration file.
public defn global-config-file (root:String) -> String :
  relative-to(root, "stanzaenv.config")

;Retrieve the default installation directory.
;1) First try $HOME/.stanzaenv
;2) Then try C:/Program Files/stanzaenv if on Windows.
public defn default-installation-dir (sys:System) -> String|False :
  val home = get-env(sys, "HOME")
  match(home:String) :
    string-join([home "/.stanzaenv"])

;Return the path to the stanzaenv executable.
public defn stanzaenv-exe-path (root:String) -> String :
  relative-to(root, relpath-to-stanzaenv())

;Return paths to all shim executables.
public defn shim-paths (root:String) -> Tuple<String> :
  [relative-to(bin-dir(root), "stanza")]

;Return the directory that all the Stanza versions are stored.
public defn stanza-version-dir (root:String) -> String :
  relative-to(root, "stanza")

;Return the path to the stanza executable given the Stanza
;installation directory.
public defn stanza-exe-path (stanza-root:String) -> String :
  relative-to(stanza-root, "stanza")

;Return the path to the Stanza directory.
public defn stanza-version-path (stanzaenv-root:String, version:NamedVersion) -> String :
  val [a,b,c] = value(version)
  val folder = to-string("v%_.%_.%_" % [a,b,c])
  relative-to(stanza-version-dir(stanzaenv-root), folder)

;Return the path to the stanzaenv temp directory.
public defn stanzaenv-temp-dir (stanzaenv-root:String) -> String :
  relative-to(stanzaenv-root, "temp")

;============================================================
;====================== Path Utilities ======================
;============================================================

;Return a new path where the relative path is relative to the given directory.
public defn relative-to (dir:String, path:String) -> String :
  to-string(relative-to-dir(parse-path(dir), path))