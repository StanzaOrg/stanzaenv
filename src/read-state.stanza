defpackage stanzaenv/read-state :
  import core
  import collections
  import core/parsed-path
  import stanzaenv/config
  import stanzaenv/config-syntax
  import stanzaenv/system-state
  import stanzaenv/system-api
  import stanzaenv/system-utils
  import stanzaenv/system-path
  import stanzaenv/versions
  import stanzaenv/paths
  import stanzaenv/errors

public defn system-state (sys:System, installation:EnvInstallation) -> SystemState :
  within wrap-error(SystemStateError) :
    ;Ensure that stanzaenv is installed.
    if installation is NoInstallation :
      throw(StanzaenvNotInstalled())
    ;Read the project-specific configuration.
    val project-config = read-project-config(sys)
    ;Read the global configuration.
    val global-config = read-global-config(sys, path(installation))
    ;Read the path status
    val path-status = read-path-status(sys, path(installation))
    ;Detect the installed versions.
    val installed-versions = detect-installed-versions(sys, path(installation))
    ;Return the system state
    SystemState(
      installation,
      project-config,      
      global-config,
      path-status,
      installed-versions)

public defn system-state (sys:System) -> SystemState :
  system-state(sys, detect-installation(sys))    

;============================================================
;============ Read the Project Stanza Version ===============
;============================================================

;Read the project stanza version.
defn read-project-config (sys:System) -> ConfigFile|False :
  value? $ for path in all-parent-directories(sys, ".") first :
    val filepath = to-string(relative-to-dir(path, project-config-file()))
    if file-exists?(sys, filepath) : One(read-config-file(sys,filepath))
    else : None()

;Retrieve all parent directories starting from 'start'.
defn all-parent-directories (sys:System, start:String) -> Seq<ParsedPath> :
  val parsed = parse-path(resolve-path!(sys, start))
  for n in reverse(1 through length(entries(parsed))) seq :
    ParsedPath(entries(parsed)[0 to n])

;============================================================
;============= Read the Global Stanza Version ===============
;============================================================

defn read-global-config (sys:System, root:String) -> ConfigFile|False :
  if file-exists?(sys, path(StanzaenvConfig(), root)) :
    read-config-file(sys, path(StanzaenvConfig(), root))

;============================================================
;=================== Detect Installation ====================
;============================================================

;Check whether stanzaenv is already installed. Returns false
;if it is not. Throws an exception if the installation is
;corrupted or error occurs during confirmation.
public defn detect-installation (sys:System) -> EnvInstallation :
  match(get-env(sys, "STANZAENV_DIR")) :
    ;Supposed to be installed to a specific directory.
    (envdir:String) :
      if file-exists?(sys, envdir) :
        if not valid-installation?(sys, envdir) :
          throw(BadConfigInstallation(envdir))
        ConfigInstallation(envdir)
      else : NoInstallation()
    ;Supposed to be installed to the default directory.
    (f:False) :
      val dir = default-installation-dir(sys)
      match(dir:String) :
        if file-exists?(sys, dir) :
          if not valid-installation?(sys, dir) :
            throw(BadDefaultInstallation(dir))
          DefaultInstallation(dir)
        else : NoInstallation()
      else : NoInstallation()

;Returns true if the given root is a valid installation.
defn valid-installation? (sys:System, root:String) -> True|False :
  val path = path(StanzaenvExe(), root)
  directory?(sys, root) and
  file-exists?(sys, path) and
  not directory?(sys, path)

;============================================================
;================= Read the Path Status =====================
;============================================================

defn read-path-status (sys:System, root:String) -> PathStatus :
  val syspaths = system-paths(sys)
  val index = find-on-path-list(sys, syspaths, path(BinFolder(), root))
  PathStatus(syspaths, index)

;============================================================
;================ Detect Installed Versions =================
;============================================================

defn detect-installed-versions (sys:System, root:String) -> Tuple<InstalledStanza> :
  val dir = path(StanzasFolder(), root)
  if file-exists?(sys, dir) :
    to-tuple $ for file in list-dir(sys, dir) seq? :
      if directory?(sys, relative-to(dir,file)) :
        match(parse-version-from-folder(file)) :
          (v:NamedVersion) : One(InstalledStanza(v))
          (f:False) : None()
      else : None()
  else :
    []

defn parse-version-from-folder (folder:String) -> NamedVersion|False :
  if prefix?(folder, "v") : parse-named-version(folder[1 to false])